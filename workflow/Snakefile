from pathlib import Path
import glob
import os
import shutil
import subprocess
import sys
import math
import yaml


include: "rules/common.smk"


from snakemake.utils import min_version, validate

min_version("6.0")




configfile: os.path.join(workflow.basedir, "../config/config.yaml")


# validate(config, "runconfig.schema.yaml")


localrules:
    all,


all_stages = ["demux", "annotate", "denoise", "preprocess"]
assert config["stage"] in all_stages, f"stage must be one the following: {all_stages}"

if config["stage"] != "annotate":
    config["asv_fasta"] = f"denoise/{config['pool']}_asvs.fasta"

if config["stage"] not in ["demux", "annotate"]:
    MANIFEST = load_manifest(
        config["manifest"],
        os.path.join(workflow.basedir, "../config/manifest.schema.yaml"),
    )


module demux:
    snakefile:
        "rules/demux.smk"
    config:
        config


if config["stage"] == "demux":
    use rule * from demux as demux_*
    config["primer_F"], config["primer_R"] = extract_primers(config)





module preprocess:
    snakefile:
        "rules/preprocess.smk"
    config:
        config
    skip_validation: True


if config["stage"] == "preprocess":
    use rule * from preprocess as preprocess_*


# note that the default pipeline
if not config["pooling"]:

    module denoise:
        snakefile:
            "rules/denoise.smk"
        config:
            config

else:

    module denoise:
        snakefile:
            "rules/denoise_pooled.smk"
        config:
            config


if config["stage"] == "denoise":
    use rule * from denoise as denoise_*


module annotate:
    snakefile:
        "rules/annotate.smk"
    config:
        config
    skip_validation: True


if config["stage"] == "annotate":
    use rule * from annotate as annotate_*


if config["skip_anno"]:
    Path("annotation_skipped.txt").touch()

    use rule all from annotate as annotate_all with:
        input:
            "annotation_skipped.txt",


onstart:
    with open("config_used.yml", "w") as outfile:
        yaml.dump(config, outfile)



rule all:
    input:
        getattr(rules, f"{config['stage']}_all").input
    default_target: True
